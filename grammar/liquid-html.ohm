LiquidHTML {
  Node
    = (HtmlNode | liquidNode | textNode)*

  HtmlNode =
    ScriptTag | VoidElement | SelfClosingElement | TagClose | TagOpen

  ScriptTag =
    "<script" AttrList #(">" scriptTagBody "</script>")

  scriptTagBody =
    anyExceptStar<"</script>">

  VoidElement =
    #("<" voidElementName) AttrList ">"

  SelfClosingElement =
    #("<" tagName) AttrList "/>"

  TagOpen =
    #("<" tagName) AttrList ">"

  TagClose =
    #("</" tagName) ">"

  tagName =
    letter (alnum | "-")*

  liquidTagName =
    letter (alnum)*

  AttrList = Attr*

  Attr =
    AttrSingleQuoted | AttrDoubleQuoted | AttrUnquoted | attrEmpty

  attrEmpty = attrName

  AttrUnquoted = attrName "=" attrUnquotedValue
  AttrSingleQuoted = attrName "=" #("'" attrSingleQuotedValue "'")
  AttrDoubleQuoted = attrName "=" #("\"" attrDoubleQuotedValue "\"")

  attrName = letter (letter | digit | "-" )+
  attrUnquotedValue = (liquidNode | attrUnquotedTextNode)+
  attrSingleQuotedValue = (liquidNode | attrSingleQuotedTextNode)+
  attrDoubleQuotedValue = (liquidNode | attrDoubleQuotedTextNode)+

  attrUnquotedTextNode = anyExceptPlus<(space | quotes | "=" | "<" | ">" | "`" | "{{" | "{%")>
  attrSingleQuotedTextNode = anyExceptPlus<("'" | "{{" | "{%")>
  attrDoubleQuotedTextNode = anyExceptPlus<("\""| "{{" | "{%")>

  quotes =  "'" | "\""

  LiquidNode = liquidNode
  liquidNode = liquidDrop | liquidTagOpen | liquidTagClose | liquidTag

  liquidTagOpen = "{%" "-"? space* blockName space* tagMarkup "-"? "%}"
  liquidTagClose = "{%" "-"? space* "end" blockName space* tagMarkup "-"? "%}"
  liquidTag = "{%" "-"? space* liquidTagName space? tagMarkup "-"? "%}"
  liquidDrop = "{{" "-"? anyExceptStar<("-}}" | "}}")> "-"? "}}"

  // https://www.w3.org/TR/2011/WD-html-markup-20110113/syntax.html#void-element
  // Cheating a bit with by stretching it to the doctype
  voidElementName =
    | caseInsensitive<"!doctype">
    | caseInsensitive<"area">
    | caseInsensitive<"base">
    | caseInsensitive<"br">
    | caseInsensitive<"col">
    | caseInsensitive<"command">
    | caseInsensitive<"embed">
    | caseInsensitive<"hr">
    | caseInsensitive<"img">
    | caseInsensitive<"input">
    | caseInsensitive<"keygen">
    | caseInsensitive<"link">
    | caseInsensitive<"meta">
    | caseInsensitive<"param">
    | caseInsensitive<"source">
    | caseInsensitive<"track">
    | caseInsensitive<"wbr">

  blockName =
    // Shopify blocks
    | "style"
    | "form"
    | "paginate"
    // Base blocks
    | "capture"
    | "case"
    | "comment"
    | "for"
    | "ifchanged"
    | "if"
    | "raw"
    | "tablerow"

  tagMarkup = anyExceptStar<("-%}"| "%}")>

  anyExcept<lit> = (~ lit any)
  anyExceptStar<lit> = (~ lit any)*
  anyExceptPlus<lit> = (~ lit any)+

  textNode = anyExcept<openControl> anyExceptStar<openControl>
  openControl = "<" | "{{" | "{%"
}
